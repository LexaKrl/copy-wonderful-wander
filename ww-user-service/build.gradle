import org.jooq.meta.jaxb.Logging

plugins {
    id 'nu.studer.jooq' version '10.0'
    id 'org.liquibase.gradle' version '2.2.0'
}

ext['jooq.version'] = jooq.version.get()

jar.enabled = false

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-jooq')
    implementation 'org.jooq:jooq-codegen'
    runtimeOnly 'org.postgresql:postgresql'

    jooqGenerator 'org.postgresql:postgresql'

    liquibaseRuntime 'org.liquibase:liquibase-core'
    liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:2.1.1'
    liquibaseRuntime 'info.picocli:picocli:4.7.5'
    liquibaseRuntime 'org.postgresql:postgresql'

    implementation project(':ww-api')
    implementation project(':ww-db')

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

jooq {
    configurations {
        main {
            generateSchemaSourceOnCompilation = true

            generationTool {
                logging = Logging.WARN
                jdbc {
                    driver = "$dbDriverClassName"
                    url = "$dbUrl"
                    user = "$dbUsername"
                    password = "$dbPassword"
                }
                generator {
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        inputSchema = 'public'
                    }
                    generate {
                        deprecated = false
                        records = true
                        immutablePojos = true
                        fluentSetters = true
                        implicitJoinPathsToMany = false
                    }
                    target {
                        packageName = 'com.technokratos'
                        directory = 'build/generated-src/jooq/main'
                    }
                    strategy.name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                }
            }
        }
    }
}

liquibase {
    activities {
        main {
            changelogFile 'ww-db/src/main/resources/db/changelog/user-service/changelog-master.xml'
            url "$dbUrl"
            username "$dbUsername"
            password "$dbPassword"
        }
    }
}

tasks.named('generateJooq') {
    dependsOn 'update'
}

test {
    useJUnitPlatform()
}