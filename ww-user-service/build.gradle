plugins {
    id 'nu.studer.jooq' version '10.0'
    id 'org.liquibase.gradle' version '2.2.0'
}

jar.enabled = false

ext {
    mapstructVersion = '1.5.5.Final'
    lombokMapstructBindingVersion = '0.2.0'
    jacksonVersion = '2.18.1'
}

ext['jooq.version'] = jooq.version.get()

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:3.1.0'
    implementation 'org.jooq:jooq-codegen'
    implementation "org.mapstruct:mapstruct:$mapstructVersion"
    annotationProcessor "org.mapstruct:mapstruct-processor:$mapstructVersion"
    runtimeOnly 'org.postgresql:postgresql'

    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'

    jooqGenerator 'org.postgresql:postgresql'

    liquibaseRuntime 'org.liquibase:liquibase-core'
    liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:2.1.1'
    liquibaseRuntime 'info.picocli:picocli:4.7.5'
    liquibaseRuntime 'org.postgresql:postgresql'

    implementation project(':ww-api')
    implementation project(':ww-db')


    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.amqp:spring-rabbit-test'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

jooq {
    configurations {
        main {
            generateSchemaSourceOnCompilation = true

            generationTool {
                jdbc {
                    driver = "org.postgresql.Driver"
                    url = "$dbUrl"
                    user = "$dbUsername"
                    password = "$dbPassword"
                }
                generator {
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        inputSchema = 'public'
                    }
                    generate {
                        deprecated = false
                        records = true
                        immutablePojos = true
                        fluentSetters = true
                        implicitJoinPathsToMany = false
                    }
                    target {
                        packageName = 'com.technokratos'
                        directory = 'build/generated-src/jooq/main'
                    }
                    strategy.name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                }
            }
        }
    }
}

liquibase {
    activities {
        main {
            changelogFile 'ww-db/src/main/resources/db/changelog/user-service/changelog-master.xml'
            url "$dbUrl"
            username "$dbUsername"
            password "$dbPassword"
        }
    }
}

tasks.named('generateJooq') {
    dependsOn 'update'
}

test {
    useJUnitPlatform()
}
